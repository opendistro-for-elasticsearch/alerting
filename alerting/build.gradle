/*
 *   Copyright 2019 Amazon.com, Inc. or its affiliates. All Rights Reserved.
 *
 *   Licensed under the Apache License, Version 2.0 (the "License").
 *   You may not use this file except in compliance with the License.
 *   A copy of the License is located at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 *   or in the "license" file accompanying this file. This file is distributed
 *   on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either
 *   express or implied. See the License for the specific language governing
 *   permissions and limitations under the License.
 */
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'elasticsearch.esplugin'
apply plugin: 'jacoco'
if (!System.properties.containsKey('tests.rest.cluster') && !System.properties.containsKey('tests.cluster')) {
    apply from: '../build-tools/esplugin-coverage.gradle'
}
ext {
    projectSubstitutions = [:]
    licenseFile = rootProject.file('LICENSE.txt')
    noticeFile = rootProject.file('NOTICE.txt')
}

if (isSnapshot) {
    version += "-SNAPSHOT"
}

esplugin {
    name 'opendistro_alerting'
    description 'Amazon OpenDistro alerting plugin'
    classname 'com.amazon.opendistroforelasticsearch.alerting.AlertingPlugin'
    extendedPlugins = ['lang-painless']
}

// Prefer elastic bundled versions for conflicts (primarily with AWS SDK).  We need to specify these manually because
// esplugin sets ResolutionStrategy.failOnVersionConflict. Luckily it is also nice enough to expose (most of) the 
// versions it uses in ext.versions.

configurations.all {
    if (it.state != Configuration.State.UNRESOLVED) return
    resolutionStrategy {
        force "joda-time:joda-time:${versions.joda}"
        force "com.fasterxml.jackson.core:jackson-core:${versions.jackson}"
        force "com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:${versions.jackson}"
        force "commons-logging:commons-logging:${versions.commonslogging}"
        force "org.apache.httpcomponents:httpcore:${versions.httpcore}"
        force "commons-codec:commons-codec:${versions.commonscodec}"
        force "commons-collections:commons-collections:3.2.2"
        force "org.apache.httpcomponents:httpcore-nio:4.4.11"
        force "org.apache.httpcomponents:httpclient:4.5.7"

        // This is required because kotlin coroutines core-1.1.1 still requires kotin stdlib 1.3.20 and we're using 1.3.21
        force "org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}"
        force "org.jetbrains.kotlin:kotlin-stdlib-common:${kotlin_version}"
    }
}

dependencies {
    compileOnly "org.elasticsearch.plugin:elasticsearch-scripting-painless-spi:${versions.elasticsearch}"
    compile "org.apache.httpcomponents:httpasyncclient:4.1.4"

    // Elasticsearch Nanny state
    compile "org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}"
    compile "org.jetbrains.kotlin:kotlin-stdlib-common:${kotlin_version}"
    compile "org.jetbrains:annotations:13.0"

    compile project(":alerting-core")
    compile project(":alerting-notification")

    testImplementation "org.jetbrains.kotlin:kotlin-test:${kotlin_version}"
}

javadoc.enabled = false // turn off javadoc as it barfs on Kotlin code
licenseHeaders.enabled = true
dependencyLicenses.enabled = false
thirdPartyAudit.enabled = false

// See package README.md for details on using these tasks.
(1..3).each { i ->
    def _numNodes = findProperty('numNodes') as Integer ?: 1
    tasks.create(name : "runMultiNode$i", type: org.elasticsearch.gradle.test.RunTask) {
        daemonize = true
        numNodes = _numNodes
        setting 'node.name', "multi-node-$i"
        setting 'http.port', '9200-9300'
        setting 'transport.tcp.port', '9300-9400'
        clusterName = 'multi-node-run'
        plugin project.path
        distribution = "oss-zip"
    }
    
    tasks.create(name: "startMultiNode$i") {
        if (_numNodes == 1) {
            dependsOn "runMultiNode${i}#start"
        } else {
            (0..<_numNodes).each { n -> dependsOn "runMultiNode${i}#node${n}.start" }
        }
    }
}
task startMultiNode(dependsOn: startMultiNode1)

def es_tmp_dir = rootProject.file('build/private/es_tmp').absoluteFile
es_tmp_dir.mkdirs()

test {
    systemProperty 'tests.security.manager', 'false'
}

integTestRunner {
    systemProperty 'tests.security.manager', 'false'
    systemProperty 'java.io.tmpdir', es_tmp_dir.absolutePath
    // Tell the test JVM if the cluster JVM is running under a debugger so that tests can use longer timeouts for
    // requests. The 'doFirst' delays reading the debug setting on the cluster till execution time.
    doFirst { systemProperty 'cluster.debug', integTestCluster.debug }

    // The --debug-jvm command-line option makes the cluster debuggable; this makes the tests debuggable
    if (System.getProperty("test.debug") != null) {
        jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=*:5005'
    }
}

integTestCluster {
    distribution = "oss-zip"
}
run {
    distribution = "oss-zip"
}

apply from: '../build-tools/pkgbuild.gradle'
