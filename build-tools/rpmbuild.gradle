apply plugin: 'nebula.ospackage'

ospackage {
    packageName = "opendistro-elasticsearch-plugin-${name}"
    release = isSnapshot ? "0.1" : '1'
    version = "${project.version}" - "-SNAPSHOT"

    into '/usr/share/elasticsearch/plugins'
    from (zipTree(bundlePlugin.outputs.files.getSingleFile())) {
        into esplugin.name
    }

    user 'root'
    permissionGroup 'root'
    fileMode 0644
    dirMode 0755

    requires('elasticsearch-oss', versions.elasticsearch, EQUAL)
    arch = 'NOARCH'
    packager = 'Amazon'
    vendor = 'Amazon'
    os = 'LINUX'
    prefix '/usr'

    license 'ASL-2.0'
    maintainer 'OpenDistro for Elasticsearch Team <opendistro@amazon.com>'
    url 'https://opendistro.github.io/elasticsearch/downloads'
    summary '''
    Alerting plugin for OpenDistro for Elasticsearch. 
    Reference documentation can be found at https://opendistro.github.io/elasticsearch/docs.
    '''.stripIndent().replace('\n', ' ').trim()
}

buildRpm {
    archiveName "${packageName}-${version}.rpm"
    dependsOn 'assemble'
}