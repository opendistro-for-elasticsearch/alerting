import io.pry.gradle.offline_dependencies.UpdateOfflineRepositoryTask

/**
 * Creates a local repo of all the dependencies required to build this plugin. This repo can be used when building in
 * sandboxed environments with limited network access.  The prepareOfflineRepo task should be run whenever a new dependency is 
 * added and the output 'offline-repo.zip' file should be put in Brazil's S3 using S3BinarySafeDownloader: 
 * https://code.amazon.com/packages/S3BinarySafeDownloader/trees/mainline
 */
if (project.findProperty("offlineRepo")) {
    task prepareOfflineRepo(type: Zip, group: 'build') {
        archiveName = "offline-repo.zip"
        destinationDir = project.file('buildSrc')
        from offlineRepositoryRoot
        into 'offline-repo'
    }

    allprojects {
        // Setup the tasks to prepare offline dependencies for all projects
        apply plugin: 'io.pry.gradle.offline_dependencies'
        offlineDependencies {
            repositories {
                mavenCentral()
                maven { url "https://plugins.gradle.org/m2/" }
            }
            includeSources = includeJavadocs = false
        }
        // Zip up the repo after all projects have added their dependencies
        tasks.withType(UpdateOfflineRepositoryTask) { task ->
            rootProject.prepareOfflineRepo.dependsOn task
        }
    }
}