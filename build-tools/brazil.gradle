/*
 *   Copyright 2019 Amazon.com, Inc. or its affiliates. All Rights Reserved.
 *
 *   Licensed under the Apache License, Version 2.0 (the "License").
 *   You may not use this file except in compliance with the License.
 *   A copy of the License is located at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 *   or in the "license" file accompanying this file. This file is distributed
 *   on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either
 *   express or implied. See the License for the specific language governing
 *   permissions and limitations under the License.
 */

task cleanUpWorkspace {
    doLast {
        buildDir.eachFile { file ->
            if (file.name != "plugins" && !file.name.startsWith("brazil-")) {
                delete(file)
            }
        }
        subprojects { Project sp ->
            if (sp.file('Config').exists()) return // Avoid other brazil pkgs; can't hurt to be safe
            sp.delete(sp.buildDir)
        }
    }
}

task cleanSymlinkBuildDir {
    doLast {
        project.delete project.file('build').listFiles() ?: []
        project.delete project.file('build')
    }
    project.tasks.findByPath('clean')?.dependsOn it
}

// See https://w.amazon.com/index.php/Coverlay/FAQ#What_are_the_languages_that_Coverlay_supports.3F
task copyCoverageForBrazil(type: Copy) {
    dependsOn project.provider { jacocoReport }
    into "$buildDir/brazil-documentation/coverage"
    from("$buildDir/reports/jacoco/jacocoReport/")
    rename 'jacocoReport.xml', 'coverage.xml'
}

// See https://w.amazon.com/index.php/BrazilBuildSystem/Concepts/UnitTestingInBrazil#Unit_Test_Upload
task copyTestReportsForBrazil(type: Copy) {
    dependsOn project.provider { jacocoReport }
    into "$buildDir/brazil-unit-tests"
    subprojects {
        from "${it.buildDir}/build/test-results"
        from "${it.buildDir}/build/reports/integTestRunnerJunit"
        include '*.xml'
    }
}

// Brazil caches the contents of the entire build workspace which includes large intermediate files inside the 
// subproject build dir. To avoid this we delete this directory at the end of the build. 
afterEvaluate {
    release.dependsOn copyTestReportsForBrazil, copyCoverageForBrazil
    if (sandboxedBuild) {
        release.finalizedBy cleanUpWorkspace
    }
}